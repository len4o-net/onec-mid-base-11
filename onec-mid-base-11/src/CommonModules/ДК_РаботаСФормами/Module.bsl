
#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьГруппуГруппаСкидкиВГруппаШапкаЛево(Форма);
		ДобавитьПолеСогласованнаяСкидкаВГруппаСкидки(Форма);
		ДобавитьКомандуИКнопкуПересчитатьСуммуВГруппаСкидки(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);   
	КонецЕсли;
 	
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_КонтактноеЛицо";
	
КонецПроцедуры  

Процедура ДобавитьГруппуГруппаСкидкиВГруппаШапкаЛево(Форма)
	
	ГруппаФормы = Форма.Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;   
	
КонецПроцедуры  

Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаСкидки(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидки);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ДК_СогласованнаяСкидкаПриИзмененииПосле"); 
	
КонецПроцедуры

Процедура ДобавитьКомандуИКнопкуПересчитатьСуммуВГруппаСкидки(Форма)
	
	Команда = Форма.Команды.Добавить("ПересчитатьСумму");
	Команда.Заголовок = "Пересчитать сумму";
	Команда.Действие = "ДК_ПересчитатьСуммуПосле";
	
	КнопкаФормы = Форма.Элементы.Добавить("ПересчитатьСумму", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидки);
	КнопкаФормы.ИмяКоманды = "ПересчитатьСумму";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	 
	
КонецПроцедуры     

#КонецОбласти
